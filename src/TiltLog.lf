/**
* Display tilt angles of accelerometer
* and time on screen and printf
* @author Abhi Gundrala
*/
target C {
    threading: false,
    build: "../scripts/build_nrf_unix.sh",
};

import IMU from "lib/IMU.lf"
import Tilt from "lib/Tilt.lf"
import Display from "lib/Display.lf"

preamble {=
    #include <stdio.h>
=}

main reactor {

    imu = new IMU();
    tilt = new Tilt();
    dx = new Display(row = 0);
    dy = new Display(row = 1);

    timer t(0, 250 msec);
    state i:int(0);

    reaction(t) -> imu.trigger, dx.message {=
        static char x[BUCKLER_DISPLAY_WIDTH + 1];
        snprintf(x, BUCKLER_DISPLAY_WIDTH + 1, "i:%d", self->i++);
        printf("t:%d", self->i);

        lf_set(imu.trigger, true);
        lf_set(dx.message, x);
    =}

    reaction(imu.acc) -> tilt.x, tilt.y, tilt.z {=
        lf_set(tilt.x, imu.acc->value.x_axis);
        lf_set(tilt.y, imu.acc->value.y_axis);
        lf_set(tilt.z, imu.acc->value.z_axis);
    =}

    reaction(tilt.xz, tilt.yz) -> dy.message {=
        static char x[BUCKLER_DISPLAY_WIDTH + 1];
        snprintf(x, BUCKLER_DISPLAY_WIDTH + 1, "xz:%.2f yz:%.2f", tilt.xz->value, tilt.yz->value);
        printf("xz:%f yz:%f\n", tilt.xz->value, tilt.yz->value);
        lf_set(dy.message, x);
    =}

}

/**
 * Template for robot driving lab exercises.
 * This template just periodically switches between a
 * STOPPED and a DRIVING mode, updating the LCD display
 * on each change of mode.
 */
target C {
    threading: false,
    build: "../scripts/build_nrf_unix.sh"   
};

import Display from "lib/Display.lf";

preamble {=
    // Include the functions for driving the robot
    // and reading its sensors.
    #include "lib/romi.h"
=}

reactor Robot {
    input drive:bool;       // Toggle mode.
    output notify:string;   // Notify of mode change.

    reaction(startup) -> notify {=
        // Initialize the robot.
        APP_ERROR_CHECK(romi_init());
        lf_set(notify, "INIT");
    =}

    initial mode STOPPED {
        reaction(drive) -> DRIVING, notify {=
            lf_set_mode(DRIVING);
            lf_set(notify, "DRIVING");
        =}
    }

    mode DRIVING {
        reaction(drive) -> STOPPED, notify {=
            lf_set_mode(STOPPED);
            lf_set(notify, "STOPPED");
        =}
    }
}

main reactor {
    timer t(0, 2 sec);
    state drive:bool(false);
    robot = new Robot();
    display = new Display();
    reaction(t) -> robot.drive {=
        lf_set(robot.drive, self->drive);
        // Toggle the drive state variable for next time.
        self->drive = !self->drive;
    =}
    robot.notify -> display.message;
}

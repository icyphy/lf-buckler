/**
 * Toggle LEDs on the nRF52 board periodically and in response to
 * a button push.
 * 
 * NOTE: This uses the legacy driver API and should be updated to use
 * nrfx_gpiote.h.
 * 
 * @author Abhi Gundrala
 */
target C {
    threading: false,
    build: "../scripts/build_nrf_unix.sh",
};

preamble {=
    #include "nrf_gpio.h"       // Defines nrf_gpio...
    #include "nrf_drv_gpiote.h" // nrf_gpio interrupt driver
    #include "nrf_log.h"
    #include "nrf_log_ctrl.h"
    #include "nrf_log_default_backends.h"
    #include "app_error.h"

    #define LED0 NRF_GPIO_PIN_MAP(0, 17)
    #define LED1 NRF_GPIO_PIN_MAP(0, 18)
    #define LED2 NRF_GPIO_PIN_MAP(0, 19)
    #define LED3 NRF_GPIO_PIN_MAP(0, 20)

    #define BTN1 NRF_GPIO_PIN_MAP(0, 13)
    // nrf interrupt
    void* p_btn_trigger;
    void btn_pin_handle(nrf_drv_gpiote_pin_t pin, nrf_gpiote_polarity_t polarity) {
        // interrupt body
        lf_schedule(p_btn_trigger, 0);
        printf("DEBUG: interrupt\n");
    }
=}

/**
 * Flash a distinctive pattern on three LEDs on the nRF52 and,
 * in response to pushing button 1, toggle the fourth LED.
 * This can be usefully included in other programs to indicate liveness.
 */
reactor FlashingLEDs {
    timer t1(0 sec, 1 sec);
    timer t2(0 sec, 2 sec);
    timer t3(0 sec, 3 sec);

    physical action btn;

    reaction(startup) -> btn {=
        // btn handle
        p_btn_trigger = btn;
        
        // Configure LEDs by defining their GPIO pins as outputs.
        // Initially, the LEDs will be on.
        ret_code_t error_code = NRF_SUCCESS;

        // Initialize GPIO driver.
        if (!nrfx_gpiote_is_init()) {
            error_code = nrfx_gpiote_init();
        }
        APP_ERROR_CHECK(error_code);

        // Configure LEDs.
        // Manually-controlled (simple) output, initially set.
        nrfx_gpiote_out_config_t out_config = NRFX_GPIOTE_CONFIG_OUT_SIMPLE(true);

        error_code = nrfx_gpiote_out_init(LED0, &out_config);
        APP_ERROR_CHECK(error_code);
        error_code = nrfx_gpiote_out_init(LED1, &out_config);
        APP_ERROR_CHECK(error_code);
        error_code = nrfx_gpiote_out_init(LED2, &out_config);
        APP_ERROR_CHECK(error_code);
        error_code = nrfx_gpiote_out_init(LED3, &out_config);
        APP_ERROR_CHECK(error_code);

        nrf_gpio_pin_set(LED3);

        // Set up config struct for a falling edge interrupt.
        // Input pin, trigger on high to low, high accuracy (disallows low-power operation).
        nrfx_gpiote_in_config_t in_config = NRFX_GPIOTE_CONFIG_IN_SENSE_HITOLO(true);
        in_config.pull = NRF_GPIO_PIN_PULLUP;
        error_code = nrfx_gpiote_in_init(BTN1, &in_config, btn_pin_handle);
        APP_ERROR_CHECK(error_code);
        
        // Enable interrupts.
        nrfx_gpiote_in_event_enable(BTN1, true);
    =}
    reaction(t1) {=
        nrf_gpio_pin_toggle(LED0);
    =}
    reaction(t2) {=
        nrf_gpio_pin_toggle(LED1);
    =}
    reaction(t3) {=
        nrf_gpio_pin_toggle(LED2);
    =}
    reaction(btn) {=
        nrf_gpio_pin_toggle(LED3);
    =}
}

main reactor {
    a = new FlashingLEDs();
}
